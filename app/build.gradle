apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.protobuf'

android {

    compileSdkVersion 27
    defaultConfig {
        applicationId "com.hedera.wallet"
        minSdkVersion 23
        targetSdkVersion 27
        versionCode 103
        versionName "1.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "version"
    productFlavors {
        prod {
            dimension "version"
            buildConfigField "boolean", "USE_TEST_NET", "false"
            buildConfigField "boolean", "USE_TEST_BRANCHIO", "false"
            buildConfigField "boolean", "ALLOW_EDITING_NET", "true"
        }
        dev {
            dimension "version"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            buildConfigField "boolean", "USE_TEST_NET", "true"
            buildConfigField "boolean", "USE_TEST_BRANCHIO", "true"
            buildConfigField "boolean", "ALLOW_EDITING_NET", "true"
        }
    }
}

dependencies {
    //  implementation 'com.google.firebase:firebase-crash:11.8.0'
    configurations {
        all*.exclude group: 'com.android.support', module: 'support-v13'
    }

    implementation 'com.madgag.spongycastle:core:1.58.0.0'
    implementation 'net.i2p.crypto:eddsa:0.3.0'

    implementation 'io.grpc:grpc-okhttp:1.14.0'
    implementation 'io.grpc:grpc-protobuf:1.0.0'
    implementation 'io.grpc:grpc-stub:1.14.0'

    implementation 'com.nanohttpd:nanohttpd-webserver:+'

    implementation 'com.google.guava:guava:24.0-android'
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    annotationProcessor 'android.arch.persistence.room:compiler:1.1.1'
    implementation 'javax.annotation:javax.annotation-api:1.3.1'

    implementation 'io.branch.sdk.android:library:2.19.3'

    implementation 'com.android.volley:volley:1.1.1'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'com.github.tony19:logback-android:1.3.0-3'

    implementation 'com.google.zxing:core:3.2.1'
    implementation 'com.journeyapps:zxing-android-embedded:3.3.0@aar'

    implementation 'com.wealthfront:magellan:1.1.0'
    implementation 'com.andrognito.pinlockview:pinlockview:2.1.0'

    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2') {
        exclude group: 'com.google.code.findbugs'
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.5.1-1"
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.0.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            javalite {}
        }
    }
}

repositories {
    mavenCentral()
}
